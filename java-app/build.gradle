plugins {
    id 'java'
    id "org.springframework.boot" version "2.3.3.RELEASE"
    id 'io.spring.dependency-management' version "1.0.10.RELEASE"
    id "com.gorylenko.gradle-git-properties" version "2.2.3"
    id "com.github.spotbugs" version "4.5.0"
    id "pmd"
}
apply plugin: 'beanstalk'
group = 'com.rcmp'
version = "${System.env.GITHUB_RUN_NUMBER ?: '0-SNAPSHOT'}" //Use GitHub Actions RUN_NUMBER if available

sourceCompatibility = '11'

repositories {
    mavenCentral()
    jcenter()
}

springBoot {
    buildInfo()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {


    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.flywaydb:flyway-core:6.4.4'
    implementation 'org.modelmapper:modelmapper:2.3.8'
    implementation 'org.apache.commons:commons-io:1.3.2'

    implementation 'org.springdoc:springdoc-openapi-ui:1.4.3'
    implementation 'org.springdoc:springdoc-openapi-webmvc-core:1.4.3'

    implementation 'com.github.michl-b:logback-msteams-appender:1.0.2'

    implementation platform('com.amazonaws:aws-java-sdk-bom:1.11.415')
    implementation 'com.amazonaws:aws-java-sdk-sns'

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.1.2'
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.10.1'

    runtimeOnly project(':npm-app')
}

bootRun {
}

beanstalk{
    profile = 'my-profile' // Only required if using .aws/credentials
    s3Endpoint = "s3-us-west-1.amazonaws.com"
    beanstalkEndpoint = "elasticbeanstalk.us-west-1.amazonaws.com"
    deployments{
        staging{

            file = tasks.jar
            application = 'mobile-web-app'
            environment = 'mobile-web-app-staging1'
        }
        prod{

            file = tasks.jar
            application = 'mobile-web-app'
            environment = 'mobile-web-app-prod2'
        }
    }

}
spotbugs {
    toolVersion = '4.1.2'
}
spotbugsMain {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}
spotbugsTest {
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test/spotbugs.html")
            stylesheet = 'fancy-hist.xsl'
        }
    }
}
test {
    useJUnitPlatform()
}
